plugins {
    id ("com.chaquo.python")
    id ("com.android.application")
    id ("org.jetbrains.kotlin.android")
    alias(libs.plugins.google.gms.google.services)
    alias(libs.plugins.google.firebase.crashlytics)
    id "org.owasp.dependencycheck" version "10.0.4"
}

// Configuraci칩n de OWASP Dependency Check
dependencyCheck {
    // Formato de reportes
    formats = ['HTML', 'JSON']

    // Usar NVD API Key si est치 disponible (desde variable de entorno o gradle.properties)
    nvd {
        apiKey = System.getenv('NVD_API_KEY') ?: project.findProperty('nvdApiKey') ?: ''
        // Tiempo de espera para la API
        delay = 0
    }

    // Configuraci칩n de an치lisis
    analyzers {
        assemblyEnabled = false
        nuspecEnabled = false
        nodeEnabled = false
        retirejs.enabled = false
    }

    // Optimizaciones de rendimiento
    skipConfigurations = ['lintClassPath', 'jacocoAgent', 'jacocoAnt']

    // Solo fallar en vulnerabilidades cr칤ticas (CVSS >= 7.0)
    failBuildOnCVSS = 7.0

    // No actualizar autom치ticamente si ya hay cach칠
    autoUpdate = false

    // Suprimir falsos positivos conocidos si es necesario
    suppressionFile = file("$projectDir/dependency-check-suppressions.xml").exists() ?
                     "$projectDir/dependency-check-suppressions.xml" : null
}

chaquopy {
    defaultConfig {}
    productFlavors { }
    sourceSets { }
}

android {
    namespace 'com.example.calculadoraimc'
    compileSdk 36

    defaultConfig {
        applicationId "com.allesterdev.imcpractico"
        minSdk 28
        targetSdk 36
        versionCode 7
        versionName "1.0.7"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Configuraci칩n de Python en Chaquopy
        python {
            version "3.10" //
            pip {
                install "pandas"

            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_LOGS", "true"
            // IDs de prueba oficiales de Google AdMob para testing
            buildConfigField "String", "ADMOB_APP_ID", "\"ca-app-pub-3940256099942544~3347511713\""
            buildConfigField "String", "ADMOB_INTERSTITIAL_ID", "\"ca-app-pub-3940256099942544/1033173712\""
            buildConfigField "String", "ADMOB_NATIVE_ADULTOS_ID", "\"ca-app-pub-3940256099942544/2247696110\""
            buildConfigField "String", "ADMOB_NATIVE_MENORES_ID", "\"ca-app-pub-3940256099942544/2247696110\""
            manifestPlaceholders = [admobAppId: "ca-app-pub-3940256099942544~3347511713"]
            // Removed applicationIdSuffix ".debug"
        }
        release {
            buildConfigField "boolean", "DEBUG_LOGS", "false"

            // 游 SEGURIDAD: IDs de producci칩n desde variables de entorno (CI/CD) o local.properties (desarrollo local)
            // Prioridad: 1) ENV vars (GitHub Actions), 2) local.properties, 3) Fallback a test IDs
            def admobAppId = System.getenv('ADMOB_APP_ID_RELEASE') ?:
                             project.findProperty('admobAppId') ?:
                             'ca-app-pub-3940256099942544~3347511713'
            def admobInterstitialId = System.getenv('ADMOB_INTERSTITIAL_ID_RELEASE') ?:
                                      project.findProperty('admobInterstitialId') ?:
                                      'ca-app-pub-3940256099942544/1033173712'
            def admobNativeAdultosId = System.getenv('ADMOB_NATIVE_ADULTOS_ID_RELEASE') ?:
                                       project.findProperty('admobNativeAdultosId') ?:
                                       'ca-app-pub-3940256099942544/2247696110'
            def admobNativeMenoresId = System.getenv('ADMOB_NATIVE_MENORES_ID_RELEASE') ?:
                                       project.findProperty('admobNativeMenoresId') ?:
                                       'ca-app-pub-3940256099942544/2247696110'

            buildConfigField "String", "ADMOB_APP_ID", "\"${admobAppId}\""
            buildConfigField "String", "ADMOB_INTERSTITIAL_ID", "\"${admobInterstitialId}\""
            buildConfigField "String", "ADMOB_NATIVE_ADULTOS_ID", "\"${admobNativeAdultosId}\""
            buildConfigField "String", "ADMOB_NATIVE_MENORES_ID", "\"${admobNativeMenoresId}\""
            manifestPlaceholders = [admobAppId: admobAppId]

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    ndkVersion '29.0.13599879 rc2'
    buildToolsVersion '34.0.0'
}

dependencies {

    implementation platform("com.google.firebase:firebase-bom:34.7.0")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.android.gms:play-services-ads:24.5.0"
    implementation("com.google.android.ump:user-messaging-platform:4.0.0")

    implementation 'androidx.activity:activity-ktx:1.12.2'
    implementation 'androidx.core:core:1.16.0'
    implementation libs.androidx.core.splashscreen
    implementation libs.androidx.core.ktx.v1101
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
}